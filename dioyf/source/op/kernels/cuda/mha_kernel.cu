//
// Created by zhou on 2026/4/12.
//
#include <base/cuda_config.h>
#include <cfloat>
#include <cub/cub.cuh>
#include "mha_kernel.cuh"

namespace kernel {

    constexpr static int thread_num = 256;

    __device__ void softmax_gpu(float* __restrict__ x, int size) {
        int tid = threadIdx.x;
        int step = blockDim.x;

        float max_val = tid < size ? x[tid] : -INFINITY;
        for (int i = tid + step; i < size; i += step) {
            if (x[i] > max_val) {
                max_val = x[i];
            }
        }

        using BlockReduce = cub::BlockReduce<float, thread_num>;
        __shared__ BlockReduce::TempStorage temp;
        __shared__ float shared_val;

        max_val = BlockReduce(temp).Reduce(max_val, cuda::maximum<float>{});

        if (threadIdx.x == 0) {
            shared_val = max_val;
        }
        __syncthreads();

        max_val = shared_val;

        float sum = 0.0f;
        for (int i = tid; i < size; i += step) {
            x[i] = expf(x[i] - max_val);
            sum += x[i];
        }

        sum = BlockReduce(temp).Sum(sum);
        if (threadIdx.x == 0) {
            shared_val = sum;
        }
        __syncthreads();

        sum = shared_val;

        for (int i = tid; i < size; i += step) {
            x[i] /= sum;
        }
    }



    __global__ void multi_head_attention_kernel(int32_t pos, int32_t seq_len, float* query,
        float* score_ptr, float* output, float* key_cache, float* value_cache,
        int32_t kv_dim, int32_t kv_mul, int32_t head_num, int32_t head_size, int32_t layer_offset) {
        int head = blockIdx.x;
        int tid = threadIdx.x;
        if (head >= head_num) {
            return;
        }

        extern __shared__ float s_query_head[];
        float scale = 1.f / sqrtf(float(head_size));
        float* query_head = query + head * head_size;

        // 预加载query到共享内存
        for (int i = tid; i < head_size; i += blockDim.x) {
            s_query_head[i] = query_head[i];
        }
        __syncthreads();

        float* score_head = score_ptr + head * seq_len;
        int head_offset = (head / kv_mul) * head_size;

        // 计算注意力分数
        for (int t = tid; t <= pos; t += blockDim.x) {
            float* key_head = key_cache + layer_offset + t * kv_dim + head_offset;

            float score = 0.f;
            for (int i = 0; i < head_size; i += 4) {
                float4 key_val = *reinterpret_cast<float4*>(key_head + i);
                float4 query_val = *reinterpret_cast<float4*>(s_query_head + i);

                score += query_val.x * key_val.x
                + query_val.y * key_val.y
                + query_val.z * key_val.z
                + query_val.w * key_val.w;
            }

            score *= scale;
            score_head[t] = score;
        }

        __syncthreads();

        softmax_gpu(score_head, pos + 1);
        __syncthreads();

        float* output_head = output + head * head_size;

        // 自注意力对value矩阵加权
        for (int i = tid; i < head_size; i += blockDim.x) {
            float value = 0.f;
            for (int t = 0; t <= pos; t++) {
                float* value_head = value_cache + layer_offset + t * kv_dim + head_offset;
                float score = score_head[t];
                value += score * value_head[i];
            }
            output_head[i] = value;
        }

    }

    void mha_kernel_cu(int32_t pos, int32_t head_num, int32_t layer_index, int32_t seq_len, int32_t kv_dim,
                int32_t kv_mul, int32_t head_size, const tensor::Tensor& mha_out,
                const tensor::Tensor& query_tensor, const tensor::Tensor& score_tensor,
                const tensor::Tensor& key_cache_tensor, const tensor::Tensor& value_cache_tensor,
                base::DeviceType device_type,const CudaConfig* config
                ) {
        int32_t layer_offset = layer_index * seq_len * kv_dim;

        float* query = const_cast<float*>(query_tensor.ptr<float>());
        float* score = const_cast<float*>(score_tensor.ptr<float>());
        float* output = const_cast<float*>(mha_out.ptr<float>());

        float* key_cache = const_cast<float*>(key_cache_tensor.ptr<float>());
        float* value_cache = const_cast<float*>(value_cache_tensor.ptr<float>());

        cudaStream_t stream_ = config->stream;
        multi_head_attention_kernel<<<head_num, thread_num, head_size * sizeof(float), stream_>>>(
            pos, seq_len, query, score, output, key_cache, value_cache, kv_dim, kv_mul, head_num,
            head_size, layer_offset);
    }
}